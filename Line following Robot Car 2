#include <LiquidCrystal.h>

LiquidCrystal lcd(8, 9, 4, 5, 6, 7);

// Motor driver pins
#define ENA 3
#define IN1 A2
#define IN2 2
#define IN3 1
#define IN4 0
#define ENB 11

// Sensors
#define LEFT_SENSOR A5
#define RIGHT_SENSOR A4

// Encoder Pins
#define RIGHT_ENCODER 13
#define LEFT_ENCODER 12

// Encoder Math
const int SLOTS_ON_DISC = 20;     
const float WHEEL_DIAMETER = 6.5; 
const float CM_PER_PULSE = (3.14159 * WHEEL_DIAMETER) / SLOTS_ON_DISC;

// Measurement Variables
volatile unsigned long leftPulseCount = 0; 
volatile unsigned long rightPulseCount = 0;
volatile byte lastPortB = 0; 

unsigned long startTime = 0;
unsigned long lastLCDUpdate = 0;
float totalDistance = 0.0; // Track total distance traveled
bool isStopped = false; // Flag to check if the car is stopped after traveling 30 cm

int speedBeforeStop = 150;
int speedAfterStop = 85;

void moveForward(int speed);
void turnLeft();
void turnRight();
void stopCar();
void updateStatsLCD();

void setup() {
  pinMode(ENA, OUTPUT);
  pinMode(ENB, OUTPUT);
  pinMode(IN1, OUTPUT);
  pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT);
  pinMode(IN4, OUTPUT);

  pinMode(LEFT_SENSOR, INPUT);
  pinMode(RIGHT_SENSOR, INPUT);

  pinMode(LEFT_ENCODER, INPUT_PULLUP);  // Left encoder pin with pull-up
  pinMode(RIGHT_ENCODER, INPUT_PULLUP); // Right encoder pin with pull-up

  // --- PIN CHANGE INTERRUPT SETUP (Port B for Pins 12 & 13) ---
  cli();                
  PCICR |= 0b00000001;  // Enable Port B
  PCMSK0 |= 0b00110000; // Mask Pin 12 & 13
  sei();  

  lastPortB = PINB; 

  lcd.begin(16, 2);
  lcd.setCursor(0, 0);
  lcd.print("Line Follower");
  delay(1500);
  lcd.clear();

  startTime = millis();
}

void loop() {
  int left  = digitalRead(LEFT_SENSOR);
  int right = digitalRead(RIGHT_SENSOR);

  // --- DISTANCE CALCULATION --- (Based on encoder pulses)
  float leftDistance = leftPulseCount * CM_PER_PULSE;
  float rightDistance = rightPulseCount * CM_PER_PULSE;
  totalDistance = (leftDistance + rightDistance) / 2.0; // Average of both wheels

  // --- TIME (seconds) ---
  unsigned long elapsed = (millis() - startTime) / 1000;

  // --- LINE FOLLOWING LOGIC ---
  if (left == HIGH && right == HIGH) {
    if (totalDistance < 29.0) {
      moveForward(speedBeforeStop); // Move forward before stop
    }
    else if (!isStopped) {
      stopCar();
      delay(2000);  // Stop for 2 seconds
      isStopped = true;
      moveForward(speedAfterStop); // After stop, continue at slower speed
    }
    else {
      moveForward(speedAfterStop); // Continue moving after stop
    }
  }
  else if (left == HIGH && right == LOW) {
    turnRight();
  }
  else if (left == LOW && right == HIGH) {
    turnLeft();
  }
  else {
    stopCar();
    if (millis() - lastLCDUpdate > 500) {
      updateStatsLCD();
      lastLCDUpdate = millis();
    }
  }

  // --- LCD DISPLAY --- (Update every 500 ms)
  if (millis() - lastLCDUpdate > 500) {
    updateStatsLCD();
    lastLCDUpdate = millis();
  }
}

// Function to move the car forward with a specified speed
void moveForward(int speed) {
  digitalWrite(IN1, HIGH);
  digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, HIGH);

  analogWrite(ENA, speed);
  analogWrite(ENB, speed);
}

void turnLeft() {
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, HIGH);
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, HIGH);

  analogWrite(ENA, 255);
  analogWrite(ENB, 255);
}

void turnRight() {
  digitalWrite(IN1, HIGH);
  digitalWrite(IN2, LOW);
  digitalWrite(IN3, HIGH);
  digitalWrite(IN4, LOW);

  analogWrite(ENA, 255);
  analogWrite(ENB, 255);
}

void stopCar() {
  analogWrite(ENA, 0);
  analogWrite(ENB, 0);
}

// --- INTERRUPT SERVICE ROUTINE (Port B) ---
ISR(PCINT0_vect) {
  byte currentPortB = PINB;          
  byte change = currentPortB ^ lastPortB; 
  
  // Check Pin 12 (Left Encoder)
  if (change & 0b00010000) {
    if (currentPortB & 0b00010000) leftPulseCount++;
  }
  
  // Check Pin 13 (Right Encoder)
  if (change & 0b00100000) {
    if (currentPortB & 0b00100000) rightPulseCount++;
  }

  lastPortB = currentPortB;
}

// Update the LCD display with time and distance
void updateStatsLCD() {
  float timeSec = (millis() - startTime) / 1000.0;

  lcd.setCursor(0, 0);
  lcd.print("Time: ");
  lcd.print(timeSec, 0);
  lcd.print("s   ");

  lcd.setCursor(0, 1);
  lcd.print("Dist: ");
  lcd.print(totalDistance, 0);  // Show the total distance
  lcd.print("cm   ");
}
